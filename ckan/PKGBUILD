# Maintainer: Patrik Plihal <patrik.plihal at gmail dot com>
# Maintainer: Taeyeon Mori <orochimarufan dot x3 at gmail dot com>

pkgname=ckan
pkgver=1.22.1
pkgrel=4
pkgdesc='All you need to find, install, and manage mods for Kerbal Space Program (ksp)'
arch=('any')
url="https://github.com/KSP-CKAN/CKAN/"
license=('MIT')
makedepends=("icoutils")
depends=('curl' 'mono')
conflicts=('ckan-git')

source=("${pkgname}-v${pkgver}.exe::https://github.com/KSP-CKAN/CKAN/releases/download/v${pkgver}/ckan.exe"
        "ckan"
        "ckan-handler.desktop")
sha256sums=('171eda7109902aaca387fac09b5d2815bd3e23ed76cc30223c6ec7095a600228'
            '259c4dae66d32ff132ee39093db567ef13d3d34e76792d3621253e573fb1d301'
            '33f113634d1104ef7085a3980894f83dfd1ec909b1b1eeaaea4686f5e99a0d2a')

package() {
  install -Dm755 "$srcdir/$pkgname-v$pkgver.exe" "$pkgdir/usr/lib/ckan/ckan.exe"
  install -Dm755 "$srcdir/ckan" "$pkgdir/usr/bin/ckan"
  wrestool -x -t 14 "$srcdir/$pkgname-v$pkgver.exe" > "$srcdir/$pkgname.ico"
  for res in 256 128 96 64 48 32 16; do
    mkdir -p "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps"
    icotool -x -h $res "$srcdir/$pkgname.ico" -o "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/ckan.png"
  done
  install -Dm755 "$srcdir/ckan-handler.desktop" "$pkgdir/usr/share/applications/ckan-handler.desktop"
}

